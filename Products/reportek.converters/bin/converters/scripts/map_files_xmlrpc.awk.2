# XMLgawk script to map files in WFD envelopes
# CDR input: xmlrpc_search_shapefile[?country=cc]
#
# Hermann, March 2010

# Load the XML extension 
@load xml

BEGIN { 
	# Set XMLMODE so that the XML parser reads strictly
	# compliant XML data. Convert characters to XMLCHARSET
	XMLMODE = 1
	XMLCHARSET = "UTF-8" 

	# Strings for file mapping
	STRING["AA"] = "_(RBDS?U?C?A?|SU)_"
	STRING["PA"] = "_PA_"
	STRING["GW"] = "_GWB?_"
	STRING["CW"] = "_CWB?_"
	STRING["LW"] = "_LWB?_"
	STRING["RW"] = "_RWB?_"
	STRING["TW"] = "_TWB?_"

	# Schemas for file mapping
	SCHEMA["AA"] = "http://water.eionet.europa.eu/schemas/dir200060ec/RBDSUCA_3p0.xsd"
	SCHEMA["PA"] = "http://water.eionet.europa.eu/schemas/dir200060ec/ProtArea_3p0.xsd"
	SCHEMA["GW"] = "http://water.eionet.europa.eu/schemas/dir200060ec/GWB_3p0.xsd"

	SCHEMA["CW"] = SCHEMA["LW"] = SCHEMA["RW"] =  SCHEMA["TW"] = \
	"http://water.eionet.europa.eu/schemas/dir200060ec/SWB_3p0.xsd"
}

# Character data will not be lost, but clear variable first
XMLSTARTELEM	{ chardata = "" }
XMLCHARDATA		{ chardata = $0 }

# Remember relevant attribute values
# XMLSTARTELEM == "file" && XMLATTR["locality"] && XMLATTR["isreleased"] {
XMLSTARTELEM == "file" && XMLATTR["locality"] {

	iup = toupper(XMLATTR["id"])
	num = split(iup, F, ".")
	loc = XMLATTR["locality"] ; LCTY[loc]
	sch = XMLATTR["schema"]
	unx = XMLATTR["unixtime"]
	url = XMLATTR["url"]

	if (sch) {

		if (unx > TIME[loc,sch] + 0) {
			FILE[loc,sch] = url
			TIME[loc,sch] = unx
		}

	} else if (F[num] == "DBF") {

		for (i in STRING) {

			str = STRING[i]

			if (iup ~ str && unx >= TIME[loc,str] + 0) {
	
				# There can be several files...
				if (unx == TIME[loc,str])
					FILE[loc,str] = FILE[loc,str] "|" url
				else
					FILE[loc,str] = url

				TIME[loc,str] = unx
			}
		}
	}
}

# Print file mapping
XMLENDELEM == "results" {

	for (i in LCTY)
		for (j in STRING) {

			key1 = i SUBSEP SCHEMA[j]
			key2 = i SUBSEP STRING[j]

			if (key1 in FILE && key2 in FILE) {

				# Handle cases with multiple dbf files
				split(FILE[key2], DBF, "|")

				for (file in DBF)

					# Print RBDSU mappings twice (2 checks)
					if (j == "AA") 
						print	"RB", FILE[key1], DBF[file] ORS \
								"SU", FILE[key1], DBF[file]

					# Avoid false _PA_GW_, _PA_LW_, etc.  mappings
					else if (! (j ~ /W/ && DBF[file] ~ /_PA_/))
						print	   j, FILE[key1], DBF[file]
			}
		}

	#	For debugging
	if (debug) {

		print ORS "+++ Elements in LCTY +++" ORS

		for (i in LCTY)
			print i

		print ORS "+++ Elements in TIME +++" ORS

		for (i in TIME)
			print i " = " TIME[i]

		print ORS "+++ Elements in FILE +++" ORS

		for (i in FILE)
			print i " = " FILE[i]
	}
}

# Print XML errors errors, if any
END {
	if (XMLERROR)
		printf("XMLERROR '%s' at row %d col %d len %d\n",
		XMLERROR, XMLROW, XMLCOL, XMLLEN)
}

# vim: tw=100 ts=4 sw=4
